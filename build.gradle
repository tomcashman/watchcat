import org.gradle.api.tasks.Exec
import org.apache.tools.ant.taskdefs.condition.Os
import org.apache.tools.ant.filters.*

buildscript {
    repositories {
        maven { url "http://repo.spring.io/libs-snapshot" }
        mavenLocal()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.0.0.RC4")
    }
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'eclipse-wtp'
	apply plugin: 'maven'

	version = "1.0.0-SNAPSHOT"
	group = "com.viridiansoftware.custodian"
	
	uploadArchives {
		repositories {
			mavenDeployer {
				repository(url: "")
			}
		}
	}
	
	repositories {
		mavenLocal()
		mavenCentral()
		maven { url "http://repo.spring.io/libs-snapshot" }
	}
}

project(":" + rootProject.name + "-collector") {
	apply plugin: 'spring-boot'

	dependencies {
		compile 'org.springframework.boot:spring-boot-starter:1.0.0.RC4'
		compile 'org.elasticsearch:elasticsearch:1.0.1'
		
		testCompile 'junit:junit:4.11'
        testCompile 'org.jmock:jmock-legacy:2.6.0'
        testCompile 'org.jmock:jmock-junit4:2.6.0'
	}
	
	jar {
    	baseName = 'custodian-collector'
	}
	
	task createDeb(type: Exec) {
		group = "Build"
		description = "Builds the debian package file"
		commandLine = [
			"fpm",
			"-s","dir",
			"-t","deb",
			"--name","custodian-collector",
			"--description","Custodian Metrics Collector",
			"--depends","oracle-java7-installer",
			"--vendor","Viridian Software",
			"--maintainer","Support Staff<support@viridiansoftware.com>",
			"--version",version,
			"--url","custodian.viridiansoftware.com",
			"--prefix","usr/share/viridiansoftware/install/custodian",
			"--inputs","fpm/config/package-inputs",
			"--force",
			"--before-install","fpm/scripts/before-install.sh",
			"--after-install","fpm/scripts/after-install.sh"
		]
	}
	
	createDeb.dependsOn assemble
}

project(":" + rootProject.name + "-web") {

}